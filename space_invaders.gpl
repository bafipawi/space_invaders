int window_width = 1000;
int window_height = 1000;
string window_title = "Space Invaders!";
int animation_speed = 95;
double window_red = 0;
double window_blue = 0;
double window_green = 0;

// Variable declaration
int num = 25;  // Number of aliens
int alien_starting_height = 600;
int alien_space = 50; // Space between aliens
int alien_increment = 20;
int alien_speed = 90;
int alien_time = 0;
int alien_width = 5; // for the side of the window
int alien_laser_increment = 2;
int alien_random;
int hero_move = 15;
int hero_laser_increment = 2;
int count_aliens = num;
int block_num = 5;
int block_count[block_num];
int i;

// Test "aliens"
pixmap aliens[num];
triangle hero(x = 250, y = 100, size = 30);
rectangle hero_laser();
rectangle alien_laser();
pixmap blocks[block_num];
textbox win_condition(x = 100, y = 20, text = "Congrats! You won! Press Q to quit.", size = .1);
textbox lose_condition(x = 100, y = 20, text = "Bummer! You lost! Press Q to quit.", size = .1);

// Animations
forward animation alien_move(rectangle alien);
forward animation hero_laser_move(rectangle hero_lasers);
forward animation alien_laser_move(rectangle alien_lasers);
forward animation block_laser(rectangle block);

initialization 
{
  win_condition.visible = false;
  win_condition.blue = 1;
  win_condition.red = 1;
  win_condition.green = 1;
  win_condition.x = 500;
  win_condition.y = 500;

  lose_condition.visible = false;
  lose_condition.blue = 1;
  lose_condition.red = 1;
  lose_condition.green = 1;
  lose_condition.x = 500;
  lose_condition.y = 100;

  // Laser initialization
  hero_laser.w = 3;
  hero_laser.h = 8;
  hero_laser.visible = false;
  hero_laser.animation_block = hero_laser_move;

  alien_laser.w = 3;
  alien_laser.h = 8;
  alien_laser.visible = false;
  alien_laser.animation_block = alien_laser_move;

  // Alien initialization
  for(i = 0; i < num; i += 1)
  {
    aliens[i].filename= "./img/space1.bmp";
    if(i == 0)
      aliens[i].animation_block = alien_move;
    if(i < alien_width)
    {
      aliens[i].x = 50 + (i*alien_space);
      aliens[i].y = alien_starting_height;
      aliens[i].user_int = i;
    }
    if(i > alien_width-1 && i < alien_width*2)
    {
      aliens[i].x = 50 + (i%5*alien_space);
      aliens[i].y = alien_starting_height-50;
      aliens[i].user_int = i;
    }
    if(i > alien_width*2 - 1 && i < alien_width*3)
    {
      aliens[i].x = 50 + (i%5*alien_space);
      aliens[i].y = alien_starting_height-100;
      aliens[i].user_int = i;
    }
    if(i > alien_width*3 - 1 && i < alien_width*4)
    {
      aliens[i].x = 50 + (i%5*alien_space);
      aliens[i].y = alien_starting_height-150;
      aliens[i].user_int = i;
    }
    if(i > alien_width*4 - 1 && i < alien_width*5)
    {
      aliens[i].x = 50 + (i%5*alien_space);
      aliens[i].y = alien_starting_height-200;
      aliens[i].user_int = i;
    }
  }

  for(i = 1; i < block_num+1; i += 1)
  {
    blocks[i-1].filename= "./img/block0.bmp";
    blocks[i-1].x = 100*i;
    blocks[i-1].y = 200;
    blocks[i-1].animation_block = block_laser;
    blocks[i-1].user_int = i-1;
  }

  hero.x = 250;
  hero.y = 100;
}

animation alien_move(rectangle alien)
{
  if(aliens[4].x + alien_width > window_width || aliens[0].x < 0 &&
    alien_time > 100)
  {
    alien_increment = -alien_increment;
    for(i = 0; i < num; i += 1)
    {
      aliens[i].y -= 20;
      alien_time = 0;
    }
    alien_speed = alien_speed * 9 / 10;
  }

  alien_random = random(25);
  for(i = 0; i < num; i += 1)
  {
    if(aliens[i].y <= 150)
      exit(0);
    if(!(alien_time % alien_speed))
    {
      aliens[i].x += alien_increment;
      if(aliens[i].filename == "./img/space1.bmp")
        aliens[i].filename = "./img/space2.bmp";
      else
        aliens[i].filename = "./img/space1.bmp";}
    if(aliens[i].visible == true)
    {
      if(alien_laser.visible == false && alien_random == i)
      {
        alien_laser.visible = true;
        alien_laser.x = aliens[i].x + aliens[i].w/2;
        alien_laser.y = aliens[i].y;
      }
    }
  }
  alien_time += 1;
}

animation hero_laser_move(rectangle hero_lasers)
{
  if(hero.visible == true)
  {
    hero_laser.y += hero_laser_increment;
    if(hero_laser.y >= window_height)
      hero_laser.visible = false;
    for(i = 0; i < num; i += 1)
    {
      if(hero_laser.visible == true)
      {
        if(hero_laser touches aliens[i])
        {
          hero_laser.visible = false;
          aliens[i].visible = false;
          aliens[i].y += window_height*2;
          count_aliens -= 1;
          if(count_aliens == 0)
            win_condition.visible = true;
        }
      }
    }
  }
}

animation alien_laser_move(rectangle alien_lasers)
{
  alien_laser.y -= alien_laser_increment;
  if(alien_laser.y <= 0)
    alien_laser.visible = false;
  if(alien_laser touches hero)
  {
    hero.visible = false;
    lose_condition.visible = true;
  }
}
  
animation block_laser(rectangle block)
{
  if(alien_laser touches block && block.visible == true)
  {
    alien_laser.visible = false;
    block_count[block.user_int] += 1;
    blocks[block.user_int].filename= "./img/block" + block_count[block.user_int] + ".bmp";
    if(block_count[block.user_int] > 3)
    {
      blocks[block.user_int].visible = false;
    }
  }
  if(hero_laser touches block && block.visible == true)
  {
    if(hero_laser.visible == true)
    {
      hero_laser.visible = false;
      block_count[block.user_int] += 1;
      blocks[block.user_int].filename= "./img/block" + block_count[block.user_int] + ".bmp";
    }
    if(block_count[block.user_int] > 3)
    {
      blocks[block.user_int].visible = false;
    }
  }
}

on leftarrow
{
  if(hero.x > 0)
    hero.x -= hero_move;
}

on rightarrow
{
  if(hero.x < window_width-20)
    hero.x += hero_move;
}

on space
{
  if(hero_laser.visible == false && hero.visible == true)
  {
    hero_laser.visible = true;
    hero_laser.x = hero.x + hero.w/2;
    hero_laser.y = hero.y + hero.h;
  }
}
